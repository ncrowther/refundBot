
	w{BaF†Êç2¸ÑcÇ,// Description
//	Access the Travel Refund website.  Enter a single refund and return the result.
// 
// Input Variables
//	in_param_ticket                   - Ticket Number to be refunded
// 	in_param_repaymentAmt    - The amount to be refunded in GBP
// 	in_param_paymentType      - The payment type. Currently restricted to CARD only
// 	in_param_refundReason     - The reason for the refund, for example: Covid
//
// Output Variables
// 	out_success   - boolean value indicating success or failure
// 	out_code       - 00 if success, any other number represents an error code
// 	out_desc        - Description of error code. For example "Website not found"
//
defVar --name in_param_ticket --type String --value M3A83727 --parameter
defVar --name in_param_repaymentAmt --type String --value "49.7" --parameter
defVar --name in_param_paymentType --type String --value Card --parameter
defVar --name in_param_refundReason --type String --value Covid --parameter
defVar --name out_success --type Boolean --value True --output
defVar --name out_code --type String --value 99 --output
defVar --name out_desc --type String --value FAILED --output
defVar --name browserId --type Browser
defVar --name focusCorpUrl --type String --value "https://focuscorpgui.eu-gb.mybluemix.net"
defVar --name ticketStatusBanner --type String
defVar --name ticketStatus --type String
defVar --name actualRepaymentAmt --type String
defVar --name subTextList --type List --innertype String
defVar --name controlId --type String
defVar --name xpath --type String
defVar --name fieldPresent --type Boolean --value False
defVar --name htmlCode --type String
defVar --name refundSuccessPresent --type Numeric
defVar --name refundErrorsPresent --type Numeric
defVar --name refundCodeStart --type Numeric --value 1
defVar --name refundCodeLen --type Numeric --value 2
defVar --name refundMessage --type String
defVar --name refundMessageStart --type Numeric --value 4
defVar --name refundCode --type String
defVar --name CancellationPresent --type Numeric
defVar --name waitForElementStatus --type Boolean
defVar --name retVal --type Boolean
defVar --name DEFAULT_TIMEOUT --type TimeSpan --value "00:00:05"

onError --executenext

goSub --label ProcessSingleRefund

beginSub --name ProcessSingleRefund
	webStart --name browser --type "Chrome" browserId=value
	
	webNavigate --url "${focusCorpUrl}" --comment "Navigate to Refund web site"
	
// Search for the ticket
	goSub --label findTicket
	
	if --left "${browserId}" --operator "Is_Null" --negate
		webClose --name browser
	endIf
endSub

beginSub --name findTicket
	
// Find the ticket to refund
	logMessage --message "Finding ticket ${in_param_ticket}" --type "Info"
	setVar --name "${out_success}" --value true
	
// Set ticket number
	goSub --label setTicketNumber
	if --left "${out_success}" --operator "Is_True" --negate
		return
	endIf
	
// Set payment type
	goSub --label setPaymentType
	
// Set refund amount
	goSub --label setRefundAmount
	
// Set refund reason
	goSub --label setRefundReason
	
// Click Refund button
	goSub --label clickRefundButton
	
// Get refund code and  message from ticket status banner
	goSub --label GetRefundResultCode
	
endSub


beginSub --name waitWebClickId
	webWaitElement --selector "Id" --id "${controlId}" --timeout "${DEFAULT_TIMEOUT}" waitForElementStatus=value
	if --left "${waitForElementStatus}" --operator "Is_True" --negate
		setVar --name "${out_success}" --value false
		setVar --name "${out_code}" --value 99
		setVar --name "${out_desc}" --value "Timeout error"
		
		logMessage --message "Failed to find element id ${controlId}" --type "Info"
		return
	endIf
	webClick --selector "Id" --id "${controlId}" --simulatehuman
endSub

beginSub --name GetRefundResultCode
	webGet --selector "XPath" --xpath "//*[@id=\"result\"]" --timeout "00:00:05" ticketStatusBanner=value
	
	if --left "${ticketStatusBanner}" --operator "Is_Empty"
		setVar --name "${out_success}" --value false
		setVar --name "${out_code}" --value 99
		setVar --name "${out_desc}" --value "Failed to process"
		return
	endIf
	
	getSubString --text "${ticketStatusBanner}" --start ${refundCodeStart} --length ${refundCodeLen} refundCode=value
	getSubString --text "${ticketStatusBanner}" --start ${refundMessageStart} refundMessage=value
	
	if --left "${refundCode}" --operator "Equal_To" --right 00
		setVar --name "${out_success}" --value true
	else
		setVar --name "${out_success}" --value false
	endIf
	
	setVar --name "${out_code}" --value "${refundCode}"
	setVar --name "${out_desc}" --value "${refundMessage}"
endSub

beginSub --name setTicketNumber
	setVar --name "${controlId}" --value ticketNumber
	goSub --label waitWebClickId
	webSet --value "${in_param_ticket}" --selector "Id" --id "${controlId}" --simulatehuman
endSub

beginSub --name setPaymentType
	setVar --name "${controlId}" --value paymentType
	goSub --label waitWebClickId
	webSet --value "${in_param_paymentType}" --selector "Id" --id "${controlId}" --simulatehuman
endSub
beginSub --name setRefundAmount
	setVar --name "${controlId}" --value amount
	goSub --label waitWebClickId
	webSet --value "${in_param_repaymentAmt}" --selector "Id" --id "${controlId}" --simulatehuman
endSub

beginSub --name setRefundReason
	setVar --name "${controlId}" --value reason
	goSub --label waitWebClickId
	webSet --value "${in_param_refundReason}" --selector "Id" --id "${controlId}" --simulatehuman
endSub

beginSub --name clickRefundButton
	setVar --name "${controlId}" --value submit
	goSub --label waitWebClickId
	delay --timeout "00:00:01"
endSub"	#w3V«jH•TbóJüåe*21.0.6.0